apply plugin: 'com.android.application'
apply plugin: 'com.squareup.sqldelight'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion

    defaultConfig {
        applicationId 'com.pushtorefresh.storio.sample'
        minSdkVersion 15
        targetSdkVersion rootProject.ext.compileSdkVersion
        versionCode 1
        versionName '1.0'

        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
    }

    signingConfigs {
        sample {
            storeFile file('keystore.jks')
            storePassword 'sample'
            keyAlias 'sample'
            keyPassword 'sample'
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.sample
            matchingFallbacks = ['release']
        }

        release {
            signingConfig signingConfigs.sample
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        disable 'InvalidPackage' // Dagger
        disable 'GoogleAppIndexingWarning'
    }

    packagingOptions {
        exclude 'LICENSE.txt' // multiple libs have this file -> cause build error
    }
}

// Ensure the no-op dependency is always used in JVM tests.
configurations.all { config ->
    if (config.name.contains('UnitTest')) {
        config.resolutionStrategy.eachDependency { details ->
            if (details.requested.group == 'com.squareup.leakcanary' && details.requested.name == 'leakcanary-android') {
                details.useTarget(group: details.requested.group, name: 'leakcanary-android-no-op', version: details.requested.version)
            }
        }
    }
}

dependencies {
    implementation libraries.storIOSQLite
    implementation libraries.storIOContentResolver

    implementation libraries.storIOSQLiteAnnotations
    annotationProcessor libraries.storIOSQLiteAnnotationsProcessor

    implementation libraries.storIOContentResolverAnnotations
    annotationProcessor libraries.storIOContentResolverAnnotationsProcessor

    implementation libraries.dagger
    annotationProcessor libraries.daggerCompiler

    implementation libraries.javaxInject
    implementation libraries.javaxAnnotationApi

    implementation libraries.rxJava2
    implementation libraries.rxAndroid2
    implementation libraries.appCompat
    implementation libraries.recyclerView
    implementation libraries.timber
    implementation libraries.butterKnife
    annotationProcessor libraries.butterKnife

    compileOnly libraries.autoParcel
    annotationProcessor libraries.autoParcelProcessor

    debugImplementation libraries.leakCanary
    releaseImplementation libraries.leakCanaryNoOp

    testImplementation libraries.junit
    testImplementation libraries.assertJ
    testImplementation libraries.robolectric
    testImplementation libraries.leakCanaryNoOp
    testAnnotationProcessor libraries.butterKnife
}
